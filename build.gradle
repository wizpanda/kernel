buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:views-gradle:1.2.7"
        classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '4.+')
    }
}

version "2.2.0"
group "com.wizpanda.plugins"

apply plugin: "idea"
apply plugin: "org.grails.grails-plugin"
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'
apply plugin: "org.grails.grails-gsp"
apply plugin: "org.grails.plugins.views-json"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "io.sentry:sentry:1.7.30"
    compile "org.hashids:hashids:1.0.1"
    compile "org.codehaus.groovy.modules.http-builder:http-builder:0.7.1"
    provided "org.grails.plugins:views-json:1.2.7"

    profile "org.grails.profiles:web-plugin"

    provided "org.grails:grails-plugin-services"
    provided "org.grails:grails-plugin-domain-class"

    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
}

bootRun {
    jvmArgs = ["-Dspring.output.ansi.enabled=always"]
}

// Enable if you wish to package this plugin as a standalone application
bootRepackage.enabled = false

// region Release configuration

jar {
    exclude "com/wizpanda/UrlMappings**"
}

publishing {
    publications {
        wpJFrogMaven(MavenPublication) {
            groupId = project.group
            artifactId = project.name
            version = project.version

            from components.java
            artifact sourcesJar
            artifact javadocJar

            pom {
                name = 'Kernel'
                description = 'A Grails plugin to provide some core functionality and utility classes'
                url = 'https://github.com/wizpanda/kernel'
                developers {
                    developer {
                        id = 'sagrawal31'
                        name = 'Shashank Agrawal'
                        email = 'creative@wizpanda.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/wizpanda/kernel.git'
                    developerConnection = 'scm:git:ssh://github.com:wizpanda/kernel.git'
                    url = 'https://github.com/wizpanda/kernel/tree/main'
                }
            }
        }
    }
}

artifactory {
    contextUrl = "https://wizpanda.jfrog.io/artifactory"
    publish {
        repository {
            repoKey = "default-maven-local"
            username = "${wpArtifactoryUsername}"
            password = "${wpArtifactoryPassword}"
        }
        defaults {
            publications('wpJFrogMaven')
            publishArtifacts = true
        }
    }
}

// endregion